"Name"			= 'ToolScript'
"Author"		= 'Pavel Lang'
"Version"		= 'v0.1'
"About"			= 'Tool scripting language'

"Case Sensitive"	= True

"Start Symbol"		= <Stm List>

! ------------------------------------------------- Sets

{ID Head}	= {Letter} + [_]
{ID Tail}	= {Alphanumeric} + [_]
{All Chars}	= {&1 .. &D7FF} + {&DC00 .. &FFEF}
{String Chars1}	= {All Chars} - ['']
{String Chars2}	= {All Chars} - ["\]
{HexNum}	= {Number} + [abcdef] + [ABCDEF]

! ------------------------------------------------- Terminals

ID				= {ID Head}{ID Tail}*
!QualifiedName	= ('global::')?{ID Head}{ID Tail}*('.'{ID Head}{ID Tail}*)+
!				| 'global::'{ID Head}{ID Tail}*


StringLiteral	= '' ( {String Chars1} | '''' )* ''
				| '"' ( {String Chars2} | '\' {Printable} )* '"'

IntLiteral		= {Number}+

DecimalLiteral	= {Digit}*'.'{Digit}+

DateTimeLiteral	= [Dd]{Digit}{Digit}{Digit}{Digit} '-' {Digit}{Digit}? '-' {Digit}{Digit}? ([Tt] {Digit}{Digit}? ':' {Digit}{Digit}? (':' {Digit}{Digit}? ('.' {Digit}{Digit}{Digit} )? )? )?
				| [Tt] {Digit}{Digit}? ':' {Digit}{Digit}? (':' {Digit}{Digit}? ('.' {Digit}{Digit}{Digit} )? )?
				| 'now'
				| 'today'
				| 'tomorrow'
				| 'yesterday'

TimeSpanLiteral	= {Digit}+ [Dd] ({Digit}+ [Hh])? ({Digit}+ [Mm])? ({Digit}+('.'{Digit}{Digit}{Digit})? [Ss])?
				| {Digit}+ [Hh] ({Digit}+ [Mm])? ({Digit}+ ('.'{Digit}{Digit}{Digit})? [Ss])?
				| {Digit}+ [Mm] ({Digit}+ ('.'{Digit}{Digit}{Digit})? [Ss])?
				| {Digit}+ ('.'{Digit}{Digit}{Digit})? [Ss]

Separator		= '--' '-'+
				| '==' '='+
				| 'SEPARATOR'

! ------------------------------------------------- Rules

Comment Start = '/*'
Comment End   = '*/'
Comment Line  = '//'

! ===================================================================
! Statements
! ===================================================================

<QualifiedName>	::= ID '.' <QualifiedName> ! cause shift-reduce conflict that is not fatal
				|	ID

<Stm>			::=	if '(' <Expr> ')' <Stm>
               	|	if '(' <Expr> ')' <Then Stm> else <Stm>         
               	|	while '(' <Expr> ')' <Stm> 
               	|	for '(' <Expr> ';' <Expr> ';' <Expr> ')' <Stm>
               	|	foreach '(' ID 'in' <Expr> ')' <Stm>
               	|	using <QualifiedName> ';'			! import .NET nodule
               	|	using StringLiteral ';'				! open file
               	|	using <QualifiedName> 'as' ID ';'	! import .NET nodule
               	|	using StringLiteral 'as' ID ';'		! open file
               	|	using '(' <Expr> ')' <Stm>			! for IDisposable
               	|	observed '(' <Expr List> ')' <Stm>
               	|	<Normal Stm>

<Then Stm>   	::=	if '(' <Expr> ')' <Then Stm> else <Then Stm>
               	|	while '(' <Expr> ')' <Then Stm> 
               	|	for '(' <Expr> ';' <Expr> ';' <Expr> ')' <Then Stm>
               	|	<Normal Stm>

<Normal Stm> 	::=	do <Stm> while '(' <Expr> ')'
               	|	switch '(' <Expr> ')' '{' <Case Stms> '}'
               	|	<Block>
               	|	<Expr> ';'
               	|	break ';'
               	|	continue ';'
               	|	return <Expr> ';'
               	|	';'              !Noop statement

<Func args>    	::=	<Func args> ',' <Func Arg>
				|	<Func Arg>
				|

<Func Arg>		::=	ID
				|	ID '=' <Expr>

<Args>       	::=	<Args> ',' <Arg>
				|	<Arg>
				|

<Arg>			::=	<Op If>
				|	ID '=' <Expr>

<Case Stms> 	::=	case <Value> ':' <Stm List> <Case Stms>
              	|	default ':' <Stm List>                  
              	|	

<Block>     	::=	'{' <Stm List> '}'

<Stm List>  	::=	<Stm> <Stm List>
              	| 	<Stm>

! ===================================================================
! Function support
! ===================================================================

<Function>		::=	'function' ID '(' <Func args> ')' <Stm>
				|	'function' '(' <Func args> ')' <Stm>

! ===================================================================
! Window support
! ===================================================================

<Widget>		::=	'WINDOW' ID <WndParam List> <Layout Block>
				|	'WINDOW' <WndParam List> <Layout Block>
				|	'WIDGET' ID <Layout Block>

<WndParam List>	::=	<WndParam> <WndParam List>	! if switched shift-reduce conflict
				|

<WndParam>		::= ID '=' <Expr> ';'
				|	ID ';'

<Layout List>	::=	<Layout List> <Layout Block>
				|

<Layout Block>	::= 'HBOX'  <WndParam List> <Layout List> 'END'
				|	'VBOX'  <WndParam List> <Layout List> 'END'
				|	'TABLE' <WndParam List> <Layout List> 'END'
				|	<Menu Block>
				|	'ref' <QualifiedName> <WndParam List>
				|	'ref' StringLiteral <WndParam List>
				|	'[' <Expr> ']' <WndParam List>

<Menu Block>	::=	'MENU' <WndParam List> <MenuItems List> 'END'

<MenuItems List> ::= <Menu Item> <MenuItems List>
				|

<Menu Item>		::= <Menu Block>
				|	'ITEM' <WndParam List>
				|	Separator


! ===================================================================
! Operator precedence
! ===================================================================

<Expr List>		::=	<Expr List> ',' <Expr>
				|	<Expr>

<Dict List>		::=	<Expr> ':' <Expr> ',' <Dict List>
				|	<Expr> ':' <Expr>
				|

<Expr>			::=	<Op If> '='   <Expr>
				|	<Op If> '+='  <Expr>
				|	<Op If> '-='  <Expr>
				|	<Op If> '*='  <Expr>
				|	<Op If> '/='  <Expr>
				|	<Op If> '<==' <Expr>
				|	<Op If> '<==>' <Expr>
				|	<Op If>

<Op If>			::=	<Op Or> '?' <Op If> ':' <Op If>
				|	<Op Or>

<Op Or>			::=	<Op Or> 'or' <Op And>
				| 	<Op And>

<Op And>		::=	<Op And> 'and' <Op Equate>
				| 	<Op Equate>

<Op Equate>		::=	<Op Equate> '==' <Op Compare>
				| 	<Op Equate> '!=' <Op Compare>
				|	<Op Compare>

<Op Compare>	::=	<Op Compare> '<'  <Op In>
				|	<Op Compare> '>'  <Op In>
               	| 	<Op Compare> '<=' <Op In>
               	| 	<Op Compare> '>=' <Op In>
				| 	<Op In>

<Op In>			::=	<Op In> 'in' <Op Add>
				|	<Op In> 'in' '<' <Op Add> ',' <Op Add> '>'
				|	<Op In> 'in' '<' <Op Add> ',' <Op Add> ')'
				|	<Op In> 'in' '(' <Op Add> ',' <Op Add> '>'
				|	<Op In> 'in' '(' <Op Add> ',' <Op Add> ')'
				|	<Op Add>

<Op Add>     	::=	<Op Add> '+' <Op Mult>
               	| 	<Op Add> '-' <Op Mult>
               	| 	<Op Mult>

<Op Mult>    	::=	<Op Mult> '*' <Op Unary>
               	|	<Op Mult> '/' <Op Unary>
               	|	<Op Mult> '%' <Op Unary>
               	|	<Op Unary>

<Op Unary>   	::=	'not'  <Op Unary>
				|	'!'    <Op Unary>
               	| 	'-'    <Op Unary>
              	|	'cast' <Op Unary> 'as' <QualifiedName>
               	|	'++'   <Op Unary>
               	|	'--'   <Op Unary>
               	|	<Op Pointer> '++'
               	|	<Op Pointer> '--'
               	|	<Op Pointer> 'is' 'null'
               	|	<Op Pointer> 'not' 'null'
               	|	<Op Pointer> 'is' 'not' 'null'
               	| 	<Op Pointer>

<Op Pointer>	::=	<Op Pointer> '.' <Value>
				| 	<Op Pointer> '->' <Value>	! if not null, lookup value else return null - safe referencing
				| 	<Op Pointer> '[' <Expr> ']'
				| 	<Op Pointer> '(' <Args> ')' ! function call
				| 	<Value>

<Value>			::=	IntLiteral
				| 	StringLiteral
				|	DecimalLiteral
				|	DateTimeLiteral
				|	TimeSpanLiteral
				|	'type' <QualifiedName>
				|	<Function>
				|	<Widget>
				|	'new' <QualifiedName> '(' <Args> ')'
				|	ID
				|	'var' ID
				|	'static' ID
				|	'(' <Expr> ')'
				|	'[' <Expr List> ']'
				|	'{' <Dict List> '}'
				|	property <Expr> ';'
				|	property <Expr> get <Expr> ';'
				|	property <Expr> get <Expr> set <Expr> ';'
				|	'dict'
				|	'list'
				|	'null'
				|	'true'
				|	'false'


