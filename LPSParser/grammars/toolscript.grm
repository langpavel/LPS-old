"Name"			= 'ToolScript'
"Author"		= 'Pavel Lang'
"Version"		= 'v0.1'
"About"			= 'Tool scripting language'

"Case Sensitive"	= True

"Start Symbol"		= <Stm List>

! ------------------------------------------------- Sets

{ID Head}	= {Letter} + [_]
{ID Tail}	= {Alphanumeric} + [_]
{All Chars}	= {&1 .. &D7FF} + {&DC00 .. &FFEF}
{String Chars1}	= {All Chars} - ['']
{String Chars2}	= {All Chars} - ["\]
{HexNum}	= {Number} + [abcdef] + [ABCDEF]

! ------------------------------------------------- Terminals

ID				= {ID Head}{ID Tail}*
!QualifiedName	= ('global::')?{ID Head}{ID Tail}*('.'{ID Head}{ID Tail}*)+
!				| 'global::'{ID Head}{ID Tail}*


StringLiteral	= '' ( {String Chars1} | '''' )* ''
				| '"' ( {String Chars2} | '\' {Printable} )* '"'

IntLiteral		= {Number}+

DecimalLiteral	= {Digit}*'.'{Digit}+

DateTimeLiteral	= [Dd]{Digit}{Digit}{Digit}{Digit} '-' {Digit}{Digit}? '-' {Digit}{Digit}? ([Tt] {Digit}{Digit}? ':' {Digit}{Digit}? (':' {Digit}{Digit}? ('.' {Digit}{Digit}{Digit} )? )? )?
				| [Tt] {Digit}{Digit}? ':' {Digit}{Digit}? (':' {Digit}{Digit}? ('.' {Digit}{Digit}{Digit} )? )?
				| 'now'
				| 'today'
				| 'tomorrow'
				| 'yesterday'

TimeSpanLiteral	= {Digit}+ [Dd] ({Digit}+ [Hh])? ({Digit}+ [Mm])? ({Digit}+('.'{Digit}{Digit}{Digit})? [Ss])?
				| {Digit}+ [Hh] ({Digit}+ [Mm])? ({Digit}+ ('.'{Digit}{Digit}{Digit})? [Ss])?
				| {Digit}+ [Mm] ({Digit}+ ('.'{Digit}{Digit}{Digit})? [Ss])?
				| {Digit}+ ('.'{Digit}{Digit}{Digit})? [Ss]

Separator		= '--' '-'+
				| '==' '='+
				| 'separator'

! ------------------------------------------------- Rules

Comment Start = '/*'
Comment End   = '*/'
Comment Line  = '//'

! ===================================================================
! Statements
! ===================================================================

<QualifiedName>	::= <QualifiedName> '.' ID
				|	ID

<Stm>			::=	if '(' <Expr> ')' <Stm>
               	|	if '(' <Expr> ')' <Then Stm> else <Stm>         
               	|	while '(' <Expr> ')' <Stm> 
               	|	for '(' <Expr> ';' <Expr> ';' <Expr> ')' <Stm>
               	|	foreach '(' ID 'in' <Expr> ')' <Stm>
               	|	using <QualifiedName> ';'			! import .NET nodule
               	|	using StringLiteral ';'				! open file
               	|	using <QualifiedName> 'as' ID ';'	! import .NET nodule
               	|	using StringLiteral 'as' ID ';'		! open file
               	|	using '(' <Expr> ')' <Stm>			! for IDisposable
               	|	observed '(' <Expr List> ')' <Stm>
               	|	<Normal Stm>

<Then Stm>   	::=	if '(' <Expr> ')' <Then Stm> else <Then Stm>
               	|	while '(' <Expr> ')' <Then Stm> 
               	|	for '(' <Expr> ';' <Expr> ';' <Expr> ')' <Then Stm>
               	|	<Normal Stm>

<Normal Stm> 	::=	do <Stm> while '(' <Expr> ')'
               	|	switch '(' <Expr> ')' '{' <Case Stms> '}'
               	|	<Block>
               	|	<Expr> ';'
               	|	break ';'
               	|	continue ';'
               	|	return <Expr> ';'
               	|	';'              !Noop statement

<Func args>    	::=	<Func args> ',' <Func Arg>
				|	<Func Arg>
				|

<Func Arg>		::=	ID
				|	ID '=' <Expr>

<Args>       	::=	<Args> ',' <Arg>
				|	<Arg>
				|

<Arg>			::=	<Op If>
				|	ID '=' <Expr>

<Case Stms> 	::=	case <Value> ':' <Stm List> <Case Stms>
              	|	default ':' <Stm List>                  
              	|	

<Block>     	::=	'{' <Stm List> '}'

<Stm List>  	::=	<Stm> <Stm List>
              	| 	<Stm>

! ===================================================================
! Database support
! ===================================================================

<ID List>		::=	<ID List> ',' ID
				|	ID
				|

<Database>		::=	'database' ID '{' <DB Tables> '}'
				|	'extends' 'database' ID '{' <DB Tables> '}'

<DB Tables>		::=	<DB Tables> <DB Table>
				|

<DB Table>		::=	'template' 'table' ID '{' <DB Columns> '}' <DB Table Attr List>
				|	'template' 'table' ID 'template' ID '{' <DB Columns> '}' <DB Table Attr List>
				|	'table' ID '{' <DB Columns> '}' <DB Table Attr List>
				|	'table' ID 'template' ID '{' <DB Columns> '}' <DB Table Attr List>
				|	'extends' 'table' ID '{' <DB Columns> '}' <DB Table Attr List>

<DB Columns>	::=	<DB Columns> <DB Column>
				|

<DB Column>		::=	ID <DB Column Type> <DB Column Attr List> ';'

<DB Column Type> ::= 'primary'
				|	'foreign' ID
				|	'foreign' ID '(' ID ')'
				|	'many' ID
				|	'many' ID 'through' ID
				|	'many' ID 'through' ID '(' ID ',' ID ')'
				|	'varchar'
				|	'varchar' '(' IntLiteral ')'
				|	'integer'
				|	'bool'
				|	'decimal' '(' IntLiteral ',' IntLiteral ')'
				|	'date'
				|	'time'
				|	'datetime'
				|	'daterange'
				|	'timerange'
				|	'datetimerange'

<DB Column Attr List> ::= <DB Column Attr List> <DB Column Attr>
				|

<DB Column Attr> ::= 'unique'
				|	'not' 'null'
				|	'index'
				|	'default' <Value>
				|	ID '=' <Value>

<DB Table Attr List> ::= <DB Table Attr List> <DB Table Attr>
				|

<DB Table Attr>	::=	'index' '(' <ID List> ')' ';'
				|	<DB Trigger Runs> 'position' DecimalLiteral <Stm>
				|	ID '=' <Value> ';'

<DB Trigger Runs> ::= <DB Trigger Runs> ',' <DB Trigger Run>
				|	<DB Trigger Run>

<DB Trigger Run> ::= 'before' 'select'
				|	'after'  'select'
				|	'before' 'insert'
				|	'after'  'insert'
				|	'before' 'update'
				|	'after'  'update'
				|	'before' 'delete'
				|	'after'  'delete'
				|	'before' 'modified'
				|	'after'  'modified'

! ===================================================================
! Function support
! ===================================================================

<Function>		::=	'function' ID '(' <Func args> ')' <Stm>
				|	'function' '(' <Func args> ')' <Stm>

! ===================================================================
! Window support
! ===================================================================

<Widget>		::=	'window' ID <WndParam List> <Layout Block>
				|	'window' <WndParam List> <Layout Block>
				|	'widget' ID <Layout Block>
				|	'widget' <Layout Block>

<WndParam List>	::=	<WndParam> <WndParam List>	! if switched shift-reduce conflict
				|

<WndParam>		::= ID '=' <Expr> ';'
				|	ID ';'

<Layout List>	::=	<Layout List> <Layout Block>
				|

<Layout Block>	::= 'hbox'  <WndParam List> <Layout List> 'end'
				|	'vbox'  <WndParam List> <Layout List> 'end'
				|	'hbuttonbox' <WndParam List> <Layout List> 'end'
				|	'vbuttonbox' <WndParam List> <Layout List> 'end'
				|	'table' <WndParam List> <Layout List> 'end'
				|	'toolbar' <WndParam List> <Layout List> 'end'
				|	'button' <WndParam List> <Layout Block>
				|	'button' <WndParam List> 'end'
				|	'toolbutton' <WndParam List> <Layout Block>
				|	'toolbutton' <WndParam List> 'end'
				|	'image' <WndParam List>
				|	StringLiteral <WndParam List>
				|	<Menu Block>
				|	'[' <Expr> ']' <WndParam List>

<Menu Block>	::=	'menu' <WndParam List> <MenuItems List> 'end'

<MenuItems List> ::= <Menu Item> <MenuItems List>
				|

<Menu Item>		::= <Menu Block>
				|	'menuitem' <WndParam List>
				|	Separator


! ===================================================================
! Operator precedence
! ===================================================================

<Expr List>		::=	<Expr List> ',' <Expr>
				|	<Expr>

<Dict List>		::=	<Expr> ':' <Expr> ',' <Dict List>
				|	<Expr> ':' <Expr>
				|

<Expr>			::=	<Widget>
				|	<Database>
				|	<Op Assign>

<Op Assign>		::=	<Op If> '='   <Expr>
				|	<Op If> '+='  <Expr>
				|	<Op If> '-='  <Expr>
				|	<Op If> '*='  <Expr>
				|	<Op If> '/='  <Expr>
				|	<Op If> '<==' <Expr>
				|	<Op If> '<==>' <Expr>
				|	<Op If>

<Op If>			::=	<Op Or> '?' <Op If> ':' <Op If>
				|	<Op Or>

<Op Or>			::=	<Op Or> 'or' <Op And>
				| 	<Op And>

<Op And>		::=	<Op And> 'and' <Op Equate>
				| 	<Op Equate>

<Op Equate>		::=	<Op Equate> '==' <Op Compare>
				| 	<Op Equate> '!=' <Op Compare>
				|	<Op Equate> 'in' <Op Compare>
				|	<Op Compare>

<Op Compare>	::=	<Op Compare> '<'  <Op Add>
				|	<Op Compare> '>'  <Op Add>
               	| 	<Op Compare> '<=' <Op Add>
               	| 	<Op Compare> '>=' <Op Add>
				| 	<Op Add>

<Op Add>     	::=	<Op Add> '+' <Op Mult>
               	| 	<Op Add> '-' <Op Mult>
               	| 	<Op Mult>

<Op Mult>    	::=	<Op Mult> '*' <Op Unary>
               	|	<Op Mult> '/' <Op Unary>
               	|	<Op Mult> '%' <Op Unary>
               	|	<Op Unary>

<Op Unary>   	::=	'not'  <Op Unary>
				|	'!'    <Op Unary>
               	| 	'-'    <Op Unary>
              	|	'cast' '(' <Op Unary> 'as' <QualifiedName> ')'
               	|	'++'   <Op Unary>
               	|	'--'   <Op Unary>
               	|	<Op Pointer> '++'
               	|	<Op Pointer> '--'
               	|	<Op Pointer> 'is' 'null'
               	|	<Op Pointer> 'not' 'null'
               	|	<Op Pointer> 'is' 'not' 'null'
               	| 	<Op Pointer>

<Op Pointer>	::=	<Op Pointer> '.' <Value>
				| 	<Op Pointer> '->' <Value>	! if not null, lookup value else return null - safe referencing
				| 	<Op Pointer> '[' <Expr> ']'
				| 	<Op Pointer> '(' <Args> ')' ! function call
				| 	<Value>

<Value>			::=	IntLiteral
				| 	StringLiteral
				|	DecimalLiteral
				|	DateTimeLiteral
				|	TimeSpanLiteral
				|	'type' '(' <QualifiedName> ')'
				|	'new' <QualifiedName> '(' <Args> ')'
				|	ID
				|	'var' ID
				|	'static' ID
				|	'(' <Expr> ')'
				|	'[' <Expr List> ']'
				|	'{' <Dict List> '}'
				|	'range' '<' <Op Add> ';' <Op Add> '>'
				|	'range' '<' <Op Add> ';' <Op Add> ')'
				|	'range' '(' <Op Add> ';' <Op Add> '>'
				|	'range' '(' <Op Add> ';' <Op Add> ')'
				|	property <Expr> ';'
				|	property <Expr> get <Expr> ';'
				|	property <Expr> get <Expr> set <Expr> ';'
				|	<Function>
				|	'dict'
				|	'list'
				|	'null'
				|	'true'
				|	'false'


