function Esc(text)
{
	return text.ToString().Replace("&","&amp;").Replace("<","&lt;").Replace(">","&gt;");
};

function ShowTestMessage(server, message, err=null)
{
	using(var w = (window title="Test spojení"; dialog=["Ok"]; iconset=(err!=null)?"gtk-dialog-error":"gtk-dialog-info";
		hbox
			image dialog_stock=(err!=null)?"gtk-dialog-error":"gtk-dialog-info"; rowspan=2; expand=false;
			vbox
				["<b>Server:</b> {0}\n{1}" / [server, message]] xalign=0; expand=false;
				scrolled visible=(err!=null); col=1; newrow; width_request=350; ["<small><tt>{0}</tt></small>" / err] xalign=0; yalign=0;
			end
		end).Create())
	{
		w.ShowDialog();
	};
};

function TestClicked()
{
	var server = server.Text;
	var result = false;
	try
	{
		var conn = new LPS.Client.ServerConnection(server);
		result = conn.Ping();
		if(result)
			ShowTestMessage(server, "Spojení navázáno");
		else
			ShowTestMessage(server, "Spojení <b>nebylo</b> navázáno.\nZkontrolujte URL adresu.", "Pravděpodobně neplatná URL");
	}
	catch(var err)
	{
		ShowTestMessage(server, "Spojení <b>nebylo</b> navázáno", Esc(err));
	}
};

var wi = (
window LOGIN
	title="LPS Přihlášení"; iconset="gtk-dialog-authentication";
	dialog=["<b>Ok</b>:1:gtk-yes:default","Zrušit:2:gtk-cancel:cancel"];
table
	image dialog_stock="gtk-dialog-authentication"; expand=false; fill=false;
	"<big><b>Přihlášení na server</b></big>"
	"Server"   xalign=0; xpadding=5; newrow; [edit] name="server"; activates_default; width_request=300; tooltip_markup="<b>Server</b>: URL adresa webové služby pro přístup k datům";
		button can_focus=false; tooltip_markup="<b>Test spojení</b>: Otestuje zda je URL adresa serveru správná a zda je server připraven pro připojení"; clicked=TestClicked;
			hbox image small_stock="gtk-connect"; "Ověřit" end
	"Login"    xalign=0; xpadding=5; newrow; [edit] name="login"; colspan=2; activates_default; tooltip_markup="<b>Login</b>: Přihlašovací jméno uživatele";
	"Heslo" xalign=0; xpadding=5; newrow; [edit] name="password"; colspan=2; activates_default; visibility=false; tooltip_markup="<b>Heslo</b>: Přihlašovací heslo uživatele";
end).Create();
var result = [wi.ShowDialog(), wi["server"].Text, wi["login"].Text, wi["password"].Text];
return result;
